(* Write functions ğšğš˜ğš•ğš_ğš–ğšŠğš™ and ğšğš˜ğš•ğš_ğšğš’ğš•ğšğšğš› that have the same signatures and
behavior as ğ™»ğš’ğšœğš.ğš–ğšŠğš™ and ğ™»ğš’ğšœğš.ğšğš’ğš•ğšğšğš› correspondingly.
Use ğ™»ğš’ğšœğš.ğšğš˜ğš•ğšğš›. Do not use pattern matching or any other list functions. *)
fun fold_map f = List.foldr (fn (x, acc) => f x :: acc ) [] 
fun fold_filter f = List.foldr (fn (x, acc) => if f x then x :: acc else acc) []

(* Write a function ğšğš—ğšğš˜ğš•ğš that takes a state transition function and an initial
 state and produces a list. On each step the current state is fed into the state
 transition function, which evaluates either to ğ™½ğ™¾ğ™½ğ™´, indicating that the result
 should contain no more elements, or to ğš‚ğ™¾ğ™¼ğ™´ pair, where pair contains the next
state and the next list element. *)
fun unfold f state =
  case f state of
      NONE => []
    | SOME (state', x) => x :: unfold f state' 

(* Write a function ğšğšŠğšŒğšğš˜ğš›ğš’ğšŠğš• that takes an integer number n and evaluates to n!.
 Your function should be a composition of ğšğš—ğšğš˜ğš•ğš and ğ™»ğš’ğšœğš.ğšğš˜ğš•ğšğš•.
 You should not use any other list functions, recursion or pattern matching. *)
val factorial = (List.foldl (fn (x, acc) => x * acc) 1) o (unfold (fn x => if x > 0 then SOME (x-1 , x) else NONE))

(* Write a function ğšğš—ğšğš˜ğš•ğš_ğš–ğšŠğš™, that behaves exactly as ğ™»ğš’ğšœğš.ğš–ğšŠğš™ and ğšğš˜ğš•ğš_ğš–ğšŠğš™, but that
would be implemented in terms of ğšğš—ğšğš˜ğš•ğš.*)
fun unfold_map f =
  let
      fun helper param =
        case param of
            [] => NONE
          | x :: xs => SOME (xs, f x)
  in
      unfold helper
  end

(* Write a function ğšğš˜_ğšğš—ğšğš’ğš• that takes three arguments, ğš, ğš™ and ğš¡, and keeps applying
ğš to ğš¡ until ğš™ ğš¡ evaluates to ğšğš›ğšğš. Upon reaching that condition, ğš (ğš (ğš ... (ğš ğš¡) ...))
is returned. *)
fun do_until f p x =
  if p x
  then x
  else do_until f p (f x) 

(* Write a function ğš’ğš–ğš™_ğšğšŠğšŒğšğš˜ğš›ğš’ğšŠğš• that has the same behavior as the ğšğšŠğšŒğšğš˜ğš›ğš’ğšŠğš• function
 described above, but is defined in terms of ğšğš˜_ğšğš—ğšğš’ğš•. *)
fun imp_factorial n = #1 (do_until (fn (acc, x) => (acc * x, x - 1)) (fn (_, x) => x = 0) (1, n))

(* Write a function ğšğš’ğš¡ğšğš_ğš™ğš˜ğš’ğš—ğš that accepts some function ğš and an initial value ğš¡,
and keeps applying ğš to ğš¡ until an ğš¡ is found such that ğš ğš¡ = ğš¡.
Note that the function must have the same domain and codomain, and that the values must
 be comparable for equality. *)
fun fixed_point f = do_until f (fn x => f x = x)

(* Square root of a real number n is a fixed point of function fn(x)=1/2(x+n/x).
 Unfortunately, for reasons rooted in the arcane art of numerical analysis,
 ğš›ğšğšŠğš•s are not comparable for equality in Standard ML.
 Write a function ğš–ğš¢_ğšœğššğš›ğš that takes a real number and evaluates to an approximation of
 its square root. You will probably need to write a version of ğšğš’ğš¡ğšğš_ğš™ğš˜ğš’ğš—ğš that uses
 "difference in absolute value less than Ïµ" as a test for equality.
 Use Ïµ=0.0001. Use the number itself as an initial guess. *)
fun my_sqrt n =
  let
      fun fixed_point f = do_until f (fn x => abs (x - f x) < 0.0001)
  in
      fixed_point (fn x => 0.5 * (x + n / x)) n
  end
