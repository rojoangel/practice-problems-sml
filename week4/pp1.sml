(* Write functions ðšðš˜ðš•ðš_ðš–ðšŠðš™ and ðšðš˜ðš•ðš_ðšðš’ðš•ðšðšŽðš› that have the same signatures and
behavior as ð™»ðš’ðšœðš.ðš–ðšŠðš™ and ð™»ðš’ðšœðš.ðšðš’ðš•ðšðšŽðš› correspondingly.
Use ð™»ðš’ðšœðš.ðšðš˜ðš•ðšðš›. Do not use pattern matching or any other list functions. *)
fun fold_map f = List.foldr (fn (x, acc) => f x :: acc ) [] 
fun fold_filter f = List.foldr (fn (x, acc) => if f x then x :: acc else acc) []

(* Write a function ðšžðš—ðšðš˜ðš•ðš that takes a state transition function and an initial
 state and produces a list. On each step the current state is fed into the state
 transition function, which evaluates either to ð™½ð™¾ð™½ð™´, indicating that the result
 should contain no more elements, or to ðš‚ð™¾ð™¼ð™´ pair, where pair contains the next
state and the next list element. *)
fun unfold f state =
  case f state of
      NONE => []
    | SOME (state', x) => x :: unfold f state' 

(* Write a function ðšðšŠðšŒðšðš˜ðš›ðš’ðšŠðš• that takes an integer number n and evaluates to n!.
 Your function should be a composition of ðšžðš—ðšðš˜ðš•ðš and ð™»ðš’ðšœðš.ðšðš˜ðš•ðšðš•.
 You should not use any other list functions, recursion or pattern matching. *)
val factorial = (List.foldl (fn (x, acc) => x * acc) 1) o (unfold (fn x => if x > 0 then SOME (x-1 , x) else NONE))

(* Write a function ðšžðš—ðšðš˜ðš•ðš_ðš–ðšŠðš™, that behaves exactly as ð™»ðš’ðšœðš.ðš–ðšŠðš™ and ðšðš˜ðš•ðš_ðš–ðšŠðš™, but that
would be implemented in terms of ðšžðš—ðšðš˜ðš•ðš.*)
fun unfold_map f =
  let
      fun helper param =
        case param of
            [] => NONE
          | x :: xs => SOME (xs, f x)
  in
      unfold helper
  end

(* Write a function ðšðš˜_ðšžðš—ðšðš’ðš• that takes three arguments, ðš, ðš™ and ðš¡, and keeps applying
ðš to ðš¡ until ðš™ ðš¡ evaluates to ðšðš›ðšžðšŽ. Upon reaching that condition, ðš (ðš (ðš ... (ðš ðš¡) ...))
is returned. *)
fun do_until f p x =
  if p x
  then x
  else do_until f p (f x) 

(* Write a function ðš’ðš–ðš™_ðšðšŠðšŒðšðš˜ðš›ðš’ðšŠðš• that has the same behavior as the ðšðšŠðšŒðšðš˜ðš›ðš’ðšŠðš• function
 described above, but is defined in terms of ðšðš˜_ðšžðš—ðšðš’ðš•. *)
fun imp_factorial n = #1 (do_until (fn (acc, x) => (acc * x, x - 1)) (fn (_, x) => x = 0) (1, n))

(* Write a function ðšðš’ðš¡ðšŽðš_ðš™ðš˜ðš’ðš—ðš that accepts some function ðš and an initial value ðš¡,
and keeps applying ðš to ðš¡ until an ðš¡ is found such that ðš ðš¡ = ðš¡.
Note that the function must have the same domain and codomain, and that the values must
 be comparable for equality. *)
fun fixed_point f = do_until f (fn x => f x = x)

(* Square root of a real number n is a fixed point of function fn(x)=1/2(x+n/x).
 Unfortunately, for reasons rooted in the arcane art of numerical analysis,
 ðš›ðšŽðšŠðš•s are not comparable for equality in Standard ML.
 Write a function ðš–ðš¢_ðšœðššðš›ðš that takes a real number and evaluates to an approximation of
 its square root. You will probably need to write a version of ðšðš’ðš¡ðšŽðš_ðš™ðš˜ðš’ðš—ðš that uses
 "difference in absolute value less than Ïµ" as a test for equality.
 Use Ïµ=0.0001. Use the number itself as an initial guess. *)
fun my_sqrt n =
  let
      fun fixed_point f = do_until f (fn x => abs (x - f x) < 0.0001)
  in
      fixed_point (fn x => 0.5 * (x + n / x)) n
  end

(* Let's reuse the binary tree data structure from practice problems for Section 2:

 ðšðšŠðšðšŠðšðš¢ðš™ðšŽ 'ðšŠ ðšðš›ðšŽðšŽ = ðš•ðšŽðšŠðš | ðš—ðš˜ðšðšŽ ðš˜ðš { ðšŸðšŠðš•ðšžðšŽ : 'ðšŠ, ðš•ðšŽðšðš : 'ðšŠ ðšðš›ðšŽðšŽ, ðš›ðš’ðšðš‘ðš : 'ðšŠ ðšðš›ðšŽðšŽ }

 Write functions ðšðš›ðšŽðšŽ_ðšðš˜ðš•ðš and ðšðš›ðšŽðšŽ_ðšžðš—ðšðš˜ðš•ðš that would serve as equivalents of
 ðšðš˜ðš•ðš and ðšžðš—ðšðš˜ðš•ðš on lists for this data structure.

HINT: This is a hard problem, but consider this: the initial value for ðšðš˜ðš•ðš corresponds
 to the base case of recursion on lists (i.e., matching []), while the function passed
 to the ðšðš˜ðš•ðš corresponds to the case when we match on ::. [] and :: correspond to ðš•ðšŽðšŠðš
 and ðš—ðš˜ðšðšŽ data constructors. Similar reasoning applies to ðšžðš—ðšðš˜ðš•ðš. You might also want to
 meditate over the signatures below if this does not provide sufficient insight. *)
datatype 'a tree = leaf
                 | node of { value : 'a, left: 'a tree, right: 'a tree }

fun tree_fold f base t =
  case t of
      leaf => base
    | node {value = v, left = l, right = r} => f (tree_fold f base l, v, tree_fold f base r)

fun tree_unfold f base =
  case f base of
      NONE => leaf
    | SOME (lstate, value, rstate) => node ({left = tree_unfold f lstate,
                                            value = value,
                                            right = tree_unfold f rstate})

(* Let's try to write a simple type inference algorithm for a very simple expression language.
 We won't deal with functions, variables or polymorphism.

 The expressions will be represented by the following data type:

 ðšðšŠðšðšŠðšðš¢ðš™ðšŽ ðšŽðš¡ðš™ðš› = ðš•ðš’ðšðšŽðš›ðšŠðš•_ðš‹ðš˜ðš˜ðš•
             | ðš•ðš’ðšðšŽðš›ðšŠðš•_ðš’ðš—ðš
             | ðš‹ðš’ðš—ðšŠðš›ðš¢_ðš‹ðš˜ðš˜ðš•_ðš˜ðš™ ðš˜ðš ðšŽðš¡ðš™ðš› * ðšŽðš¡ðš™ðš›
             | ðš‹ðš’ðš—ðšŠðš›ðš¢_ðš’ðš—ðš_ðš˜ðš™ ðš˜ðš ðšŽðš¡ðš™ðš› * ðšŽðš¡ðš™ðš›
             | ðšŒðš˜ðš–ðš™ðšŠðš›ðš’ðšœðš˜ðš— ðš˜ðš ðšŽðš¡ðš™ðš› * ðšŽðš¡ðš™ðš›
             | ðšŒðš˜ðš—ðšðš’ðšðš’ðš˜ðš—ðšŠðš• ðš˜ðš ðšŽðš¡ðš™ðš› * ðšŽðš¡ðš™ðš› * ðšŽðš¡ðš™ðš›

 The data constructors represent literal booleans, literal integers, binary operators on booleans,
 binary operators on integers, comparison operators and conditionals. Since we're only interested
 in types, and not in actually evaluating our expressions, we're omitting immaterial details, such
 as whether a literal boolean is "true" or "false", or whether an operator on integers is addition,
 subtraction or something else entirely.

 The types will be represented by the following simple datatype:

 ðšðšŠðšðšŠðšðš¢ðš™ðšŽ ðšŽðš¡ðš™ðš›_ðšðš¢ðš™ðšŽ = ðšðš¢ðš™ðšŽ_ðš‹ðš˜ðš˜ðš• | ðšðš¢ðš™ðšŽ_ðš’ðš—ðš

 The typing rules for our expression language are simple:

 Literal booleans are of type ðšðš¢ðš™ðšŽ_ðš‹ðš˜ðš˜ðš•.
 Literal integers have type ðšðš¢ðš™ðšŽ_ðš’ðš—ðš.
 Boolean operators have type ðšðš¢ðš™ðšŽ_ðš‹ðš˜ðš˜ðš• provided that both of their operands also have type ðšðš¢ðš™ðšŽ_ðš‹ðš˜ðš˜ðš•.
 Integer operators have type ðšðš¢ðš™ðšŽ_ðš’ðš—ðš provided that both operands also have type ðšðš¢ðš™ðšŽ_ðš’ðš—ðš.
 Comparison operators have type ðšðš¢ðš™ðšŽ_ðš‹ðš˜ðš˜ðš• provided that both operands have type ðšðš¢ðš™ðšŽ_ðš’ðš—ðš.
 Conditionals have the same type as the first branch, provided that the second branch has the same type,
 and the condition has type ðšðš¢ðš™ðšŽ_ðš‹ðš˜ðš˜ðš•.
 Write a function ðš’ðš—ðšðšŽðš›_ðšðš¢ðš™ðšŽ that accepts an ðšŽðš¡ðš™ðš› and evaluates to the type of the given expression.
 If the type cannot be determined according to the rules above, raise ðšƒðš¢ðš™ðšŽð™´ðš›ðš›ðš˜ðš› exception. *)
datatype expr = literal_bool | literal_int
                | binary_bool_op of expr * expr | binary_int_op of expr * expr
                | comparison of expr * expr
                | conditional of expr * expr * expr

datatype expr_type = type_bool | type_int

exception TypeError

fun infer_type expression =
  case expression of
      literal_bool => type_bool
    | literal_int => type_int
    | binary_bool_op (x1, x2) => if infer_type x1 = type_bool
                                    andalso
                                    infer_type x2 = type_bool
                                 then type_bool
                                 else raise TypeError
    | binary_int_op (x1, x2) => if infer_type x1 = type_int
                                   andalso
                                   infer_type x2 = type_int
                                then type_int
                                else raise TypeError
    | comparison (x1, x2) => if infer_type x1 = type_int
                                andalso
                                infer_type x2 = type_int
                             then type_bool
                             else raise TypeError
    | conditional (x1, x2, x3) => let val t2 = infer_type x2
                                      val t3 = infer_type x3
                                  in
                                      if infer_type x1 = type_bool andalso t2 = t3
                                      then t2
                                      else raise TypeError
                                  end
